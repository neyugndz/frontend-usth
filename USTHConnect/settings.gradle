pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google.*")
                includeGroupByRegex("androidx.*")
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { // For com.github.chrisbanes:PhotoView
            url = uri('https://www.jitpack.io')
        }

        def localSdkPath = gradle.properties['LinphoneSdkBuildDir']
        def localSdkMetadata = file("${localSdkPath}/maven_repository/org/linphone/linphone-sdk-android/maven-metadata.xml")

        if (localSdkMetadata.exists()) {
            println "Using locally built SDK from maven repository at ${localSdkPath}/maven_repository/"
            maven {
                name = 'local linphone-sdk maven repository'
                url = uri("file://${localSdkPath}/maven_repository/")
                content {
                    includeGroup('org.linphone')
                }
            }
        } else {
            println 'Using CI built SDK from maven repository at https://linphone.org/maven_repository'
            maven {
                name = 'linphone.org maven repository'
                url = uri('https://linphone.org/maven_repository')
                content {
                    includeGroup('org.linphone')
                }
            }
        }
    }
}

rootProject.name = "USTH Connect"
include ':app'
